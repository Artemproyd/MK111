## Лексемы:
- **Идентификаторы (переменные), числа, строковые литералы, комментарии**
- **Операторы присваивания:** `=`
- **Скобки:** `(`, `)`, `{`, `}`, `[`, `]`
- **Арифметические операторы:** `+`, `-`, `*`, `/`, `%`
- **Операторы сравнения:** `<`, `>`, `==`, `!=`
- **Логические операторы:** `&&`, `||`, `!`
- **Специальные символы:** `;`, `,`, `.`
- **Ключевые слова:** `int`, `float`, `char`, `if`, `else`, `while`, `for`, `return`, `void`, `struct`, `input`, `output`

## Таблица переходов автомата:
### Состояния:
- `S` — начальное
- `ID` — идентификаторы и ключевые слова
- `NUM` — числа
- `STR` — строковые литералы
- `COM` — комментарии
- `OP` — операторы
- `ERR` — ошибка
- `FIN` — конечное

### Таблица

| Сост. | буква | цифра | +    | -    | *    | /    | %    | =    | <    | >    | !    | &    | \|   | ;    | ,    | .    | (    | )    | {    | }    | [    | ]    | "    | '    | пробел | \n   | др.  | EOF  |
|:------|:------|:------|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-----|:-------|:-----|:-----|:-----|
| S     | ID/1  | NUM/2 | OP/3 | OP/4 | OP/5 | OP/6 | OP/7 | OP/8 | OP/9 | OP/10| OP/11| OP/12| OP/13| FIN/14| FIN/15| OP/16| FIN/17| FIN/18| FIN/19| FIN/20| FIN/50| FIN/51| STR/21| STR/22| S/23  | S/24 | ERR/25| FIN/26|
| ID    | ID/27 | ID/27 | FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| FIN/28| ERR/25| FIN/26|
| NUM   | ERR/25| NUM/29| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| NUM/31| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| FIN/30| ERR/25| FIN/26|
| STR   | STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| STR/32| FIN/33| FIN/34| STR/32| STR/32| STR/32| ERR/25|
| OP    | FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/40| OP/41| FIN/35| FIN/35| OP/44| OP/45| OP/46| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| FIN/35| ERR/25| FIN/26|
| COM   | COM/47| COM/47| COM/47| COM/47| COM/48| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/47| COM/49| COM/47| ERR/25|

### Семантические программы
| №   | Описание                                   |
| --- | ------------------------------------------ |
| 1   | Начало идентификатора или ключевого слова  |
| 2   | Начало числового литерала                  |
| 3   | Оператор `+`                               |
| 4   | Оператор `-`                               |
| 5   | Оператор `*`                               |
| 6   | Оператор `/` или начало комментария        |
| 7   | Оператор `%`                               |
| 8   | Оператор `=`                               |
| 9   | Оператор `<`                               |
| 10  | Оператор `>`                               |
| 11  | Оператор `!`                               |
| 12  | Оператор `&`                               |
| 13  | Оператор `|`                               |
| 14  | Символ `;`                                 |
| 15  | Символ `,`                                 |
| 16  | Символ `.`                                 |
| 17  | Символ `(`                                 |
| 18  | Символ `)`                                 |
| 19  | Символ `{`                                 |
| 20  | Символ `}`                                 |
| 21  | Начало строкового литерала `"`             |
| 22  | Начало символьного литерала `'`            |
| 23  | Пропуск пробела                            |
| 24  | Пропуск перевода строки                    |
| 25  | Ошибка: недопустимый символ                |
| 26  | Конец файла                                |
| 27  | Продолжение идентификатора                 |
| 28  | Завершение идентификатора                  |
| 29  | Продолжение числа                          |
| 30  | Завершение числа                           |
| 31  | Десятичная точка в числе                   |
| 32  | Символ в строковом литерале                |
| 33  | Завершение строкового литерала             |
| 34  | Завершение символьного литерала            |
| 35  | Завершение оператора                       |
| 40  | Завершение оператора `%`                   |
| 41  | Составной оператор `==`                    |
| 44  | Составной оператор `!=`                    |
| 45  | Составной оператор `&&`                    |
| 46  | Составной оператор `||`                    |
| 47  | Продолжение комментария                    |
| 48  | Возможное завершение комментария           |
| 49  | Завершение однострочного комментария       |
| 50  | Символ `[`                                 |
| 51  | Символ `]`                                 |

## КС-грамматика языка (лексемы как терминалы)
```
Программа → ОбъявлениеФункции Программа
          | ОбъявлениеПеременной Программа
          | ОбъявлениеСтруктуры Программа
          | ε

ОбъявлениеФункции → Тип Идентификатор ( СписокПараметров ) ТелоФункции

Тип → int 
     | float 
     | char 
     | void 
     | struct Идентификатор

СписокПараметров → Параметр , СписокПараметров
                  | Параметр
                  | ε

Параметр → Тип Идентификатор
          | Тип Идентификатор [ ]

ТелоФункции → { Операторы }

Операторы → Оператор Операторы
          | ε

Оператор → ОбъявлениеПеременной
         | ПрисваиваниеПеременной
         | УсловныйОператор
         | ЦиклWhile
         | ЦиклFor
         | ОператорReturn
         | ОператорВвода
         | ОператорВывода
         | { Операторы }

ОбъявлениеПеременной → Тип Идентификатор ;
                      | Тип Идентификатор = Выражение ;
                      | Тип Идентификатор [ Число ] ;

ОбъявлениеСтруктуры → struct Идентификатор { СписокПолей } ;

СписокПолей → Тип Идентификатор ; СписокПолей
             | ε

ПрисваиваниеПеременной → Идентификатор = Выражение ;
                        | Идентификатор . Идентификатор = Выражение ;
                        | Идентификатор [ Выражение ] = Выражение ;

ОператорВвода → input ( Идентификатор ) ;
               | input ( Идентификатор [ Выражение ] ) ;

ОператорВывода → output ( Выражение ) ;

УсловныйОператор → if ( Выражение ) Оператор ElsePart

ElsePart → else Оператор
         | ε

ЦиклWhile → while ( Выражение ) Оператор

ЦиклFor → for ( ПрисваиваниеПеременной Выражение ; Выражение ) Оператор

ОператорReturn → return Выражение ;
                | return ;

Выражение → Слагаемое ВыражениеПрод

ВыражениеПрод → + Слагаемое ВыражениеПрод
               | - Слагаемое ВыражениеПрод
               | ε

Слагаемое → Множитель СлагаемоеПрод

СлагаемоеПрод → * Множитель СлагаемоеПрод
               | / Множитель СлагаемоеПрод
               | % Множитель СлагаемоеПрод
               | ε

Множитель → Идентификатор
          | Идентификатор . Идентификатор
          | Идентификатор [ Выражение ]
          | Число
          | СтроковыйЛитерал
          | СимвольныйЛитерал
          | ( Выражение )
          | Идентификатор ( СписокАргументов )
          | ! Выражение

СписокАргументов → Выражение , СписокАргументов
                  | Выражение
                  | ε

ЛогическоеВыражение → Выражение ОператорСравнения Выражение ЛогическоеВыражениеПрод
                     | ( ЛогическоеВыражение ) ЛогическоеВыражениеПрод
                     | ! ЛогическоеВыражение ЛогическоеВыражениеПрод
                     | Выражение ЛогическоеВыражениеПрод

ЛогическоеВыражениеПрод → && ЛогическоеВыражение ЛогическоеВыражениеПрод
                         | || ЛогическоеВыражение ЛогическоеВыражениеПрод
                         | ε

ОператорСравнения → <
                  | >
                  | ==
                  | !=
```

## КС-грамматика в нестрогой форме Грейбах
```
Программа → int Идентификатор ( СписокПараметров ) ТелоФункции Программа
          | float Идентификатор ( СписокПараметров ) ТелоФункции Программа
          | char Идентификатор ( СписокПараметров ) ТелоФункции Программа
          | void Идентификатор ( СписокПараметров ) ТелоФункции Программа
          | struct Идентификатор { СписокПолей } ; Программа
          | int Идентификатор ОкончаниеОбъявления Программа
          | float Идентификатор ОкончаниеОбъявления Программа
          | char Идентификатор ОкончаниеОбъявления Программа
          | ε

ОкончаниеОбъявления → ;
                     | = Выражение ;
                     | [ Число ] ;

СписокПараметров → Тип Идентификатор ОбъявлениеМассиваВПараметре ПродолжениеСписка
                  | ε

ОбъявлениеМассиваВПараметре → [ ]
                             | ε

ПродолжениеСписка → , СписокПараметров
                   | ε

Тип → int 
     | float 
     | char 
     | void 
     | struct Идентификатор

СписокПолей → Тип Идентификатор ; СписокПолей
             | ε

ТелоФункции → { Операторы }

Операторы → int Идентификатор ОкончаниеОбъявления Операторы
          | float Идентификатор ОкончаниеОбъявления Операторы
          | char Идентификатор ОкончаниеОбъявления Операторы
          | Идентификатор ПродолжениеПрисваивания Операторы
          | if ( Выражение ) Оператор ElsePart Операторы
          | while ( Выражение ) Оператор Операторы
          | for ( ПрисваиваниеПеременной Выражение ; Выражение ) Оператор Операторы
          | return ОкончаниеReturn Операторы
          | input ( ИдентификаторДляВвода ) ; Операторы
          | output ( Выражение ) ; Операторы
          | { Операторы } Операторы
          | ε

ElsePart → else Оператор
         | ε

ИдентификаторДляВвода → Идентификатор
                       | Идентификатор [ Выражение ]

ПродолжениеПрисваивания → = Выражение ;
                          | . Идентификатор = Выражение ;
                          | [ Выражение ] = Выражение ;

ОкончаниеReturn → Выражение ;
                 | ;

Оператор → int Идентификатор ОкончаниеОбъявления
         | float Идентификатор ОкончаниеОбъявления
         | char Идентификатор ОкончаниеОбъявления
         | Идентификатор ПродолжениеПрисваивания
         | if ( Выражение ) Оператор ElsePart
         | while ( Выражение ) Оператор
         | for ( ПрисваиваниеПеременной Выражение ; Выражение ) Оператор
         | return ОкончаниеReturn
         | input ( ИдентификаторДляВвода ) ;
         | output ( Выражение ) ;
         | { Операторы }

ПрисваиваниеПеременной → Идентификатор = Выражение ;

Выражение → Идентификатор ПродолжениеВыражения
          | Число ПродолжениеВыражения
          | СтроковыйЛитерал ПродолжениеВыражения
          | СимвольныйЛитерал ПродолжениеВыражения
          | ( Выражение ) ПродолжениеВыражения
          | ! Выражение ПродолжениеВыражения
          | ε ПродолжениеВыражения

ПродолжениеВыражения → + Выражение ПродолжениеВыражения
                      | - Выражение ПродолжениеВыражения
                      | * Выражение ПродолжениеВыражения
                      | / Выражение ПродолжениеВыражения
                      | % Выражение ПродолжениеВыражения
                      | < Выражение ЛогическоеПродолжение
                      | > Выражение ЛогическоеПродолжение
                      | == Выражение ЛогическоеПродолжение
                      | != Выражение ЛогическоеПродолжение
                      | && Выражение ЛогическоеПродолжение
                      | || Выражение ЛогическоеПродолжение
                      | . Идентификатор ПродолжениеВыражения
                      | [ Выражение ] ПродолжениеВыражения
                      | ( СписокАргументов ) ПродолжениеВыражения
                      | ε

ЛогическоеПродолжение → && Выражение ЛогическоеПродолжение
                       | || Выражение ЛогическоеПродолжение
                       | ε

СписокАргументов → Выражение ПродолжениеСпискаАргументов
                  | ε

ПродолжениеСпискаАргументов → , Выражение ПродолжениеСпискаАргументов
                             | ε
```

## Семантические действия для генерации ОПС (Обратной Польской Записи)

Согласно лекциям, ОПС генерируется в следующем формате:
- Сначала операнды (переменные, константы)
- Затем операторы
- Для условных операторов используются метки и переходы

```
<ОбъявлениеПеременной> → int Идентификатор {генерировать(ИД.имя)} ;
<ОбъявлениеПеременной> → int Идентификатор {сохранить(ИД)} = <Выражение> {генерировать(ИД.имя, ":=")} ;

<ПрисваиваниеПеременной> → Идентификатор {сохранить(ИД)} = <Выражение> {генерировать(ИД.имя, ":=")} ;

<УсловныйОператор> → if ( <Выражение> {метка M1; генерировать(M1, "jf")} ) <Оператор> {генерировать(M1 + ":")}
<УсловныйОператор> → if ( <Выражение> {метка M1; генерировать(M1, "jf")} ) <Оператор> {метка M2; генерировать(M2, "j")} else <Оператор> {генерировать(M1 + ":", M2 + ":")}

<ЦиклWhile> → while {метка M0; генерировать(M0 + ":")} ( <Выражение> {метка M1; генерировать(M1, "jf")} ) <Оператор> {генерировать(M0, "j", M1 + ":")}

<Выражение> → <Слагаемое>
<Выражение> → <Выражение> + <Слагаемое> {генерировать("+")}
<Выражение> → <Выражение> - <Слагаемое> {генерировать("-")}

<Слагаемое> → <Множитель>
<Слагаемое> → <Слагаемое> * <Множитель> {генерировать("*")}
<Слагаемое> → <Слагаемое> / <Множитель> {генерировать("/")}
<Слагаемое> → <Слагаемое> % <Множитель> {генерировать("%")}

<Множитель> → Идентификатор {генерировать(ИД.имя)}
<Множитель> → Число {генерировать(Число.значение)}
<Множитель> → ( <Выражение> )

<ЛогическоеВыражение> → <Выражение> < <Выражение> {генерировать("<")}
<ЛогическоеВыражение> → <Выражение> > <Выражение> {генерировать(">")}
<ЛогическоеВыражение> → <Выражение> == <Выражение> {генерировать("==")}
<ЛогическоеВыражение> → <Выражение> != <Выражение> {генерировать("!=")}
```

## Список операций ОПС

### Арифметические операции:
- **+** — сложение
- **-** — вычитание
- ***" — умножение
- **/** — деление
- **%** — остаток от деления

### Операции сравнения:
- **>** — больше
- **<** — меньше
- **==** — равно
- **!=** — не равно

### Операции управления:
- **:=** — присваивание
- **jf** — условный переход (jump if false)
- **j** — безусловный переход (jump)
- **метка:** — объявление метки

### Операции ввода-вывода:
- **r** — чтение (read)
- **w** — запись (write)

## Формат ОПС

ОПС записывается в виде последовательности элементов через пробел:
```
операнд1 операнд2 ... операция
```

### Примеры генерации ОПС:

**1. Простое присваивание:** `int x = 42;`
```
ОПС: 42 x :=
```

**2. Арифметическое выражение:** `int y = 10 + 5;`
```
ОПС: 10 5 + y :=
```

**3. Условный оператор:** `if (x > 5) { y = 10; }`
```
ОПС: x 5 > m1 jf 10 y := m1:
```

**4. Условный оператор с else:** `if (a > b) { a = b; } else { b = a; }`
```
ОПС: a b > m1 jf a b := m2 j m1: b a := m2:
```

**5. Цикл while:** `while (a > b) { a = b; }`
```
ОПС: m0: a b > m1 jf a b := m0 j m1:
```

**6. Сложное выражение:** `(x + y) * (a - b)`
```
ОПС: x y + a b - *
```

**7. Ввод и вывод:** `input(a); output(a * 2);`
```
ОПС: a r a 2 * w
```

## Алгоритм вычисления ОПС

ОПС вычисляется с использованием стека:

1. **Операнд** — помещается в стек
2. **Операция** — извлекает нужное количество операндов из стека, выполняет операцию, результат помещает в стек
3. **jf** — извлекает значение из стека, если оно равно 0 (ложь), переходит на указанную метку
4. **j** — безусловный переход на метку
5. **:=** — извлекает значение из стека и присваивает переменной

### Пример вычисления: `x 5 > m1 jf 10 y := m1:`

При x = 3:
1. `x` → стек: [3]
2. `5` → стек: [3, 5]
3. `>` → стек: [0] (3 > 5 = false = 0)
4. `m1 jf` → стек: [], переход на m1 (т.к. 0 = false)
5. Пропускаем `10 y :=`
6. `m1:` → продолжение выполнения

При x = 7:
1. `x` → стек: [7]
2. `5` → стек: [7, 5]  
3. `>` → стек: [1] (7 > 5 = true = 1)
4. `m1 jf` → стек: [], не переходим (т.к. 1 = true)
5. `10` → стек: [10]
6. `y :=` → стек: [], y = 10
7. `m1:` → продолжение выполнения